# Author: mosra <mosra@centrum.cz>
pkgname=nacl-magnum
pkgver=dev.glibc
pkgrel=1
pkgdesc="C++11 and OpenGL 2D/3D graphics engine (NaCl glibc version)"
arch=('any')
url="http://mosra.cz/blog/magnum.php"
license=('MIT')
depends=('nacl-corrade')
makedepends=('nacl-sdk' 'cmake' 'corrade' 'ninja')
options=(!buildflags !strip)

build() {
    # Build 32bit
    mkdir -p "$startdir/build-nacl-glibc-x86-32"
    cd "$startdir/build-nacl-glibc-x86-32"

    cmake .. \
        -DCMAKE_MODULE_PATH="$startdir/toolchains/modules" \
        -DCMAKE_TOOLCHAIN_FILE="$startdir/toolchains/generic/NaCl-glibc-x86-32.cmake" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/nacl \
        -DWITH_MAGNUMINFO=OFF \
        -DWITH_NACLAPPLICATION=ON \
        -DWITH_MAGNUMFONT=ON \
        -DWITH_MAGNUMFONTCONVERTER=ON \
        -DWITH_TGAIMAGECONVERTER=ON \
        -DWITH_TGAIMPORTER=ON \
        -DWITH_WAVAUDIOIMPORTER=ON \
        -DLIB_SUFFIX=/32 \
        -G Ninja
    ninja

    # Build 64bit
    mkdir -p "$startdir/build-nacl-glibc-x86-64"
    cd "$startdir/build-nacl-glibc-x86-64"

    cmake .. \
        -DCMAKE_MODULE_PATH="$startdir/toolchains/modules" \
        -DCMAKE_TOOLCHAIN_FILE="$startdir/toolchains/generic/NaCl-glibc-x86-64.cmake" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/nacl \
        -DWITH_MAGNUMINFO=OFF \
        -DWITH_NACLAPPLICATION=ON \
        -DWITH_MAGNUMFONT=ON \
        -DWITH_MAGNUMFONTCONVERTER=ON \
        -DWITH_TGAIMAGECONVERTER=ON \
        -DWITH_TGAIMPORTER=ON \
        -DWITH_WAVAUDIOIMPORTER=ON \
        -G Ninja
    ninja
}

package() {
    # Install 32bit
    cd "$startdir/build-nacl-glibc-x86-32"
    DESTDIR="$pkgdir/" ninja install

    # Install 64bit (the headers will be overwritten, but they are (and should
    # be) the same for both versions
    cd "$startdir/build-nacl-glibc-x86-64"
    DESTDIR="$pkgdir/" ninja install
}
